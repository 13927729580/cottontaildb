group 'ch.unibas.dmi.dbis'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.8"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'antlr'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

generateGrammarSource {
    outputDirectory = file("build/generated/source/antlr/main/java/ch/unibas/dmi/dbis/cottontail/sql/antlr")
}

sourceSets {
    main {
        proto {
            srcDir {
                "$projectDir/cottontaildb-proto/src/main/protobuf"
            }
        }
        kotlin {
            srcDirs += "$projectDir/build/generated/source/antlr/main/java"
            srcDirs += "$projectDir/build/generated/source/proto/main/java"
        }
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/build/generated/source/proto"
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.18.0'
        }
    }
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    generateProtoTasks {
        all()*.plugins { grpc {
            outputSubDir = 'java'
        }}
    }
}

dependencies {
    ///// Antlr 4 dependency
    antlr group: 'org.antlr', name: 'antlr4', version: '4.7.2'
    
    ///// MapDB dependency
    compile group: 'org.mapdb', name: 'mapdb', version: '3.0.7'

    /////// GRPC & ProtoBuf
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.2'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.6.1'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.18.0'

    ////// Log4J
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    ////// Dexecutor
    compile  group: 'com.github.dexecutor', name: 'dexecutor-core', version: '2.1.0'

    ///// JUnit 5
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'

    ///// Kotlin
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.21'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.21'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.1.1'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime', version: '0.9.1'
}
